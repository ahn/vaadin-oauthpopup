apply plugin: 'base'
apply plugin: 'maven-publish'

def vaadinVersion = '8.0.6'
ext.vaadinVersion = vaadinVersion
def scribeVersion = '2.7.3'
ext.scribeVersion = scribeVersion

task wrapper(type: Wrapper) {
   gradleVersion = '3.5'
}

allprojects {
	apply plugin: 'java'
	group = 'org.vaadin.addon.oauthpopup'
	version = '1.0.1'
	
	javadoc {
		enabled = false;
	}
}

subprojects {
	apply plugin: 'eclipse'
	apply plugin: 'war'
	
	repositories {
		mavenCentral()
		mavenLocal()
	}
	
	dependencies {
		//compile 'log4j:log4j:1.2.17'
	    compile 'com.github.scribejava:scribejava-apis:' + scribeVersion
	    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
	}
	
	eclipse {
		jdt {
			sourceCompatibility=1.8
			targetCompatibility=1.8
		}
	}
	
	// Always rebuild
	jar.outputs.upToDateWhen { false }
}

project(':oauthpopup') {

	apply plugin: 'maven-publish'
	
	dependencies {
		testCompile group: 'junit', name: 'junit', version: '4.8.1'
	    compile 'com.github.scribejava:scribejava-apis:' + scribeVersion
		compile 'com.vaadin:vaadin-server:' + vaadinVersion
		compile('com.vaadin:vaadin-push:' + vaadinVersion)
	}
	
	publishing {
	    repositories {
	        maven {
	            url "$buildDir/repo"
	        }
	    }
	    publications {
	        vaadinDirectory(MavenPublication) {
	            from components.java
	        }
	    }
	}
	
	model {
		tasks.generatePomFileForVaadinDirectoryPublication {
			destination = file("$buildDir/pom.xml")
		}
	}
	
	task jar(type: Jar, dependsOn: classes, overwrite: true) {
		baseName = project.name
		appendix = ''
		
		manifest.attributes 'Implementation-Title': 'OAuth2 Popup Add-on',
							'Implementation-Version': version,
							'Implementation-Vendor': 'Bryson Dunn',
							'Vaadin-Package-Version': '1'
							
		from(sourceSets.main.output)
		from(sourceSets.main.allSource)
		into('META-INF/maven/' + project.group + '/' + project.name) {
			from(file("$buildDir/pom.xml"))
		}
	}
	
	jar.dependsOn('generatePomFileForVaadinDirectoryPublication')
	
	artifacts {
		archives jar
	}
}